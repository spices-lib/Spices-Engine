/**
* @file Shader.BasePassRenderer.Mesh.Default.mesh.
* @brief This Shader Defines BasePass Renderer Mesh SubPass Default Mesh Shader Behaver.
* @author Spices.
*/

/************************************Pre Compile*******************************************/

#version 460
#extension GL_EXT_mesh_shader                 : require   /* @brief Declear this as mesh shader. */
#extension GL_GOOGLE_include_directive        : enable    /* @brief Enable include feature.      */
#extension GL_EXT_control_flow_attributes     : require   /* @brief Enable flow butter control.  */

#include "Header/ShaderPreRendererLayout.glsl"

/*****************************************************************************************/

/*************************************Mesh Input******************************************/

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

/**
* @brief task padload receive from task shader.
*/
taskPayloadSharedEXT uint workgroupId;    /* @brief mesh shader hanled meshlut id(as workgroupid). */

/*****************************************************************************************/

/*************************************Mesh Output*****************************************/

/**
* @brief Output triangles meshlut per workgroup.
*/
layout(triangles, max_vertices = MESHLUTNVERTICES, max_primitives = MESHLUTNPRIMITIVES) out;

/**
* @brief Mesh Shader Output to Fragment Shader.
*/
layout(location = 0) out Pixel pixel[];               /* @brief Pixel Data.     */
layout(location = 4) out flat uint meshlutId[];       /* @brief Meshlut ID.     */

/*****************************************************************************************/

/*********************************Push Constant*******************************************/

/**
* @brief push constant.
*/
layout(push_constant) uniform Push {
	PushConstantMesh push;   /* @see PushConstantMesh. */
};

/*****************************************************************************************/

/************************************Specific Data****************************************/

layout(buffer_reference, scalar, buffer_reference_align = 8) buffer Vertices { 
    Vertex v[];           
};

layout(buffer_reference, scalar, buffer_reference_align = 8) buffer Indices { 
    ivec3 i[]; 
};

layout(buffer_reference, scalar, buffer_reference_align = 8) buffer Meshluts {
    Meshlut i[];
};

/*****************************************************************************************/

/**********************************Shader Entry*******************************************/

void main()
{
    uint id = workgroupId;

    Vertices vertices   = Vertices(push.desc.vertexAddress);
    Indices indices     = Indices(push.desc.indexAddress);
    Meshlut meshlut     = Meshluts(push.desc.meshlutAddress).i[id];
    
    mat4 mvp = view.projection * view.view * push.model;
    mat3 m3model = mat3(transpose(inverse(push.model)));

    [[unroll]]
    for(uint i = 0; i < meshlut.nVertices; i++)
    {
        Vertex vt = vertices.v[meshlut.vertexOffset + i];

        gl_MeshVerticesEXT[i].gl_Position = mvp * vec4(vt.position, 1.0f);

        pixel[i].position  = vec3(push.model * vec4(vt.position, 1.0f));
        pixel[i].normal    = normalize(m3model * vt.normal);
        pixel[i].color     = vt.color;
        pixel[i].texCoord  = vt.texCoord;
        meshlutId[i]       = id;
    }

	[[unroll]]
    for(uint i = 0; i < meshlut.nPrimitives; i++)
    {
        ivec3 index = indices.i[meshlut.primitiveOffset + i] - ivec3(meshlut.vertexOffset);
        gl_PrimitiveTriangleIndicesEXT[i] =  uvec3(index.x, index.y, index.z);
    }
    
    SetMeshOutputsEXT(meshlut.nVertices, meshlut.nPrimitives);
}

/*****************************************************************************************/